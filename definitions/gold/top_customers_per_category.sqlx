config {
  type: "view",
  schema: "thelook_ecommerce_dataform_gold",
  description: "Top customers per product category",
  tags: ["gold_layer"],
}

WITH sales_per_cust_category AS (
SELECT
  u.id AS customer_id,
  u.email AS  email,
  p.category as category,
  ROUND(SUM(nc_oi.sale_price * o.num_of_item), 2) AS total_purchase
FROM ${ref("non_cancelled_or_returned_order_items")} AS nc_oi
  INNER JOIN ${ref("orders")} AS o
  ON nc_oi.order_id = o.order_id
  INNER JOIN ${ref("users")} AS u
  ON o.user_id = u.id
  INNER JOIN ${ref("products")} AS p
  ON nc_oi.product_id = p.id
GROUP BY customer_id, email, category
),
sales_per_cust_category_ranked AS (
SELECT  *, ROW_NUMBER() OVER (PARTITION BY category ORDER BY total_purchase DESC) AS rank 
FROM sales_per_cust_category
)
SELECT * from 
sales_per_cust_category_ranked
WHERE rank <= 3
ORDER BY category,total_purchase DESC